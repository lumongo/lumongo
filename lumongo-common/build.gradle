apply from: '../maven.gradle'

defaultTasks 'compileProto', 'build'

description 'Lumongo Common'

dependencies {
    compile "org.mongodb:mongo-java-driver:$mongoJavaVersion"
    compile 'com.google.guava:guava:18.0'
    compile 'com.googlecode.protobuf-rpc-pro:protobuf-rpc-pro-duplex:3.3.4'
	compile 'com.google.protobuf:protobuf-java:3.0.0'
	compile 'com.google.protobuf:protobuf-java-util:3.0.0'


	compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-log4j12:1.7.2'
    compile 'commons-pool:commons-pool:1.6'
    compile 'com.jcraft:jzlib:1.1.3'
}


sourceSets {
	main { java { srcDir 'gen-src' } }
}


task compileProto {
	doLast {
		File outputDir = project.file("gen-src")
		File protoDir = project.file("proto")

		String os = System.getProperty("os.name").toLowerCase();
		String protocCommand = "protoc";

		println("OS: " + os);
		if (os.contains("linux")) {
			protocCommand = rootProject.file("util" + File.separator +  "protoc").toString();
		}
		else {
			println("Using protoc on path");
		}

		if (!outputDir.exists()) {
			outputDir.mkdir();
		}

		if (protoDir.isDirectory() && protoDir.exists()) {
		
			String[] protos = protoDir.list()
		
			for (String proto : protos) {
				if (proto.endsWith(".proto")) {
					String protoFull = protoDir.getAbsolutePath() + File.separator + proto
					println("Compiling proto in <" + protoFull + "> to <" + outputDir.getAbsolutePath() + ">")
					exec {
						setStandardOutput(System.out)
						executable protocCommand
						args '--proto_path=' + protoDir.getAbsolutePath(), '--java_out=' + outputDir.getAbsolutePath(), protoFull
					}
				}
			}
		}
		else {
			println("Error: " + protoDir.getAbsolutePath() + " does not exist or is not a directory")
		}
	}
}

compileJava.dependsOn compileProto
eclipseProject.dependsOn compileProto



